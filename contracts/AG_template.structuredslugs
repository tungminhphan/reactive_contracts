[INPUT]
x1:0...4
y1:0...4
x2:0...4
y2:0...4
bridge
box:0...3
carry1

[OUTPUT]
left1
right1
up1
down1
left2
right2
up2
down2

[SYS_INIT]
! left1
! right1
! up1
! down1
! left2
! right2
! up2
! down2

[ENV_TRANS]
# can't pick or or drop off unless at deposit locations
(x1!=1 | y1!=0) & (x1!=3 | y1 != 3) & (x1!=1| y1!=4) -> (carry1' & carry1 | !carry1' & !carry1)
# box only changes states as a consequence of being dropped off/picked up or expiration
(box!=box' & box!=3 & box !=2) -> (x1=1 & y1=0) | (x1=3 & y1 =3) | (x1=1 & y1=4)

# picking up at 1
(x1=1 & y1=0 & !carry1 & box=1) -> (box'=0 & carry1')
# dropping off at 1
(x1=1 & y1=0 & carry1 & box=0) -> (box'=1 & !carry1')
# dropping off at 2
(x1=1 & y1=4 & carry1 & box=0) -> (box'=2 & !carry1')
# dropping off at 3
(x1=3 & y1=3 & carry1 & box=0) -> (box'=3 & !carry1')
# expirable
box=3 -> (box'=1 | box'=3)
box=2 -> (box'=1 | box'=2)

[SYS_TRANS]
# allow only one motion per step for robot 1
(! up1' & ! down1' & ! right1') |  (! up1' & ! down1' &  ! left1') | (! up1'  &  ! right1' &  ! left1') | (! down1' & ! left1' & ! right1')

# allow only one motion per step for robot 2
(! up2' & ! down2' & ! right2') |  (! up2' & ! down2' &  ! left2') | (! up2'  &  ! right2' &  ! left2') | (! down2' & ! left2' & ! right2')

# robot1 can't swim
!(x1 = 0 & y1 = 2)
!(x1 = 2 & y1 > 1 & y1 < 4)
!(x1 > 2 & y1 < 3)

# robot2 can't swim
!(x2 = 0 & y2 = 2)
!(x2 = 2 & y2 > 1 & y2 < 4)
!(x2 > 2 & y2 < 3)

# the two robots should never collide
x1 != x2 | y1 != y2

# the two robots should never cross
x1' != x2 | y1' != y2
x1 != x2' | y1 != y2'

# robots must use bridge
!bridge -> (x2 != 1 | y2 != 2)
!bridge -> (x1 != 1 | y1 != 2)

[ENV_TRANS]
# control of robot1
x1>0 & left1 -> x1'+1=x1
x1<4 & right1 -> x1'=x1+1
y1>0 & down1 -> y1'+1=y1
y1<4 & up1 -> y1'=y1+1
(!left1 & !right1) -> x1'=x1
(!up1 & !down1) -> y1'=y1

# control of robot2
x2>0 & left2 -> x2'+1=x2
x2<4 & right2 -> x2'=x2+1
y2>0 & down2 -> y2'+1=y2
y2<4 & up2 -> y2'=y2+1
(!left2 & !right2) -> x2'=x2
(!up2 & !down2) -> y2'=y2

# bridge won't be deployed when neither of the buttons are pressed
((x1 != 0 | y1 != 1) & (x2 != 0 | y2 !=1) & (x1 != 0 | y1 != 3) & (x2 != 0 | y2 != 3)) -> !bridge'

[ENV_LIVENESS]
box!=3
box!=2

[ENV_INIT]
x1!=x2 | y1!=y2

